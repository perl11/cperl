#!/usr/bin/env perl

=head1 NAME

Porting/sync-with-cpan - Synchronize with CPAN distributions

=head1 SYNOPSIS

    perl Porting/sync-with-cpan [OPTIONS] <module>

where <module> is the name which appears in the C<%Modules> hash
of F<Porting/Maintainers.pl>.

    perl Porting/sync-with-cpan -n --all | tee sync.log

lists all modules which need to be updated.

=head1 DESCRIPTION

Script to help out with syncing cpan distros.

Does the following:

=over 4

=item *

Fetches the package list from CPAN. Finds the current version of the given
package. [1]

=item *

Downloads the relevant tarball; unpacks the tarball. [1]

=item *

Clean out the old directory (C<git clean -dfx>)

=item *

Moves the old directory out of the way, moves the new directory in place.

=item *

Restores any F<.gitignore> file.

=item *

Removes files from C<@IGNORE> and C<EXCLUDED>

=item *

C<git add> any new files.

=item *

C<git rm> any files that are gone.

=item *

Remove the +x bit on files in F<t/>

=item *

Remove the +x bit on files that don't have it enabled in the current dir

=item *

Restore files mentioned in C<CUSTOMIZED>

=item *

Updates the contents of F<MANIFEST>

=item *

Updates the F<Porting/Maintainers.pl> entry

=item *

Adds new package versions to C<Module-Corelist>

=item *

Adds new F<perlcdelta> list entry

=item *

Cleans up

=item *

Runs a C<make test_prep> (assumes a configure has been run)

=item *

Runs tests for the package

=item *

Runs the porting tests

=back

[1]  If the C<--tarball> option is given, then CPAN is not consulted.
C<--tarball> should be the path to the tarball; the version is extracted
from the filename -- but can be overwritten by the C<--version> option.

=head1 OPTIONS

=over 4

=item C<--all>

Check all cpan and dist modules from Maintainers.pl, not just
the module given on the command line.

=item C<-n> or C--dryrun>

=item C<--tarball> FILE

=item C<--version> VERSION

=item C<-m> or C<--make-c>

Do a cheap C<make -C cpan/$module> without
a <make test_prep>.

Then C<perl t/harness cpan/$module/t/*.t>
and <make test_porting>.

=item C<--jobs> I<N>

When running C<make>, pass a C<< -jI<N> >> option to it.

=item C<--force>

=item C<--help>

=back

=head1 TODO

=over 4

=item *

3-way merge of CUSTOMIZED files.

=item *

Optional, run a full test suite

=item *

Handle complicated C<FILES>

=back

This is a crude early version; no attempt has been made yet to make
this portable. It shells out instead of trying to find a Perl
solution.  In particular, it assumes C<git>, C<perl>, C<grep>, C<make>
and an internet connection to be available.

=cut


package Maintainers;

use 5.016;
use strict;
use warnings;
use Getopt::Long;
use Archive::Tar;
use File::Path qw( remove_tree );
use File::Find;
use File::Spec::Functions qw( tmpdir );
use Config qw( %Config ); # for make and _exe only. so it can be another perl

$| = 1;

use constant WIN32 => $^O eq 'MSWin32';

die "This does not look like a top level directory"
     unless -d "cpan" && -d "Porting";

# Check that there's a Makefile, if needed; otherwise, we'll do most of our
# work only to fail when we try to run make, and the user will have to
# either unpick everything we've done, or do the rest manually.
die "Please run Configure before using $0\n"
    if !WIN32 && !-f "Makefile";

our (@IGNORABLE, %Modules);
my $MAKE_LOG = 'make.log';
my $maintainers = 'Porting/Maintainers.pl';

require "./".$maintainers;

my %IGNORABLE = map {$_ => 1} @IGNORABLE;
my $tmpdir = tmpdir();

my $package      = "02packages.details.txt";
my $package_url  = "http://www.cpan.org/modules/$package";
my $package_file = "$tmpdir/$package"; # this is a cache

my @problematic = (
    'podlators', # weird CUSTOMIZED section due to .PL files
);


sub usage {
    my $err = shift and select STDERR;
    print "Usage: $0 module [args] [cpan package]\n";
    exit $err;
}

GetOptions ('tarball=s'  =>  \my $tarball,
            'version=s'  =>  \my $version,
            'all'        =>  \my $all,
            'm|make-c'   =>  \my $make_c,
            'n|dryrun'   =>  \my $dryrun,
            'jobs=i'     =>  \my $make_jobs,
             force       =>  \my $force,
             help        =>  sub { usage 0; },
             ) or  die "Failed to parse arguments";

usage 1 unless $all || @ARGV;

sub find_type_f {
    my @res;
    find( { no_chdir => 1, wanted => sub {
        my $file = $File::Find::name;
        return unless -f $file;
        push @res, $file
    }}, @_ );
    @res
};

# Equivalent of `chmod a-x`
sub de_exec {
    for my $filename ( @_ ) {
        my $mode= (stat $filename)[2] & 0777;
        if( $mode & 0111 ) { # exec-bit set
            chmod $mode & 0666, $filename;
        };
    }
}

# Equivalent of `chmod +w`
sub make_writable {
    for my $filename ( @_ ) {
      my $mode = (stat $filename)[2] & 0777;
      if (!($mode & 0222)) { # not writable
        chmod $mode | (0222 & ~umask), $filename;
      }
    }
}

sub make {
    my @args = @_;
    unshift @args, "-j$make_jobs" if defined $make_jobs;
    if (WIN32) {
        chdir "Win32";
        system "$Config{make} @args> ..\\$MAKE_LOG 2>&1"
            and die "Running make failed, see $MAKE_LOG";
        chdir '..';
    } else {
        system "$Config{make} @args> $MAKE_LOG 2>&1"
            and die "Running make failed, see $MAKE_LOG";
    };
};

sub update_corelist {
  my ($module, $new_version) = @_;
  my $perlversion = "$]";
  $perlversion .= 'c' if -f 'STATUS.md';
  my $corelist = 'dist/Module-CoreList/lib/Module/CoreList.pm';
  print "Update $corelist ";
  open my $core, '<', $corelist;
  open my $newcore, '>', $corelist.'.new';
  my ($found, $in_section);
  while (<$core>) {
    my $mod;
    if (!$found) {
      if ($in_section) {
        # sorted
        if (/^\s+'(.*)'\s*=>\s+'(\d.*)',/) {
          $mod = $1;
          if ($mod eq $module) {
            if (s/$2/$new_version/) {
              $found = 1;
            }
          } elsif ($mod =~ /^(B::Op_private|Module::Core)/) {
            ;
          } elsif (fc($mod) gt fc($module)) {
            my $l = 21 - length($module);
            $l = 1 if $l < 1;
            printf $newcore "%s'%s'%s => '%s',\n", " "x12,
              $module, " "x$l, $new_version;
            $found = 1;
          }
        }
        if (/^\s+'5.0/) {
          $in_section = 0;
        }
      }
      if (/^\s+'$perlversion' =>/) {
        $in_section = 1;
      }
    }
    print $newcore $_;
  }
  close $core;
  close $newcore;
  if ($dryrun) {
    my $cmd = "diff -bu $corelist $corelist.new";
    say $cmd;
    say `$cmd`;
  }
  elsif ($found) {
    unlink $corelist;
    rename "$corelist.new" => $corelist;
    chmod 0644 => $corelist;
    say "ok";
  }
  else {
    say "failed for $module $new_version";
    say "Make sure you update $corelist before committing.";
  }
}

my @modules  = $all ? sort keys %Modules : @ARGV;

MODULES:
for my $module (@modules) {

  if (!exists $Modules{$module}) {
    if ($module =~ /-/ && $module !~ /::/) {
      $module =~ s/-/::/g;
    }
  }
  my $info         = $Modules{$module} or die "Cannot find module $module";
  my $distribution = $$info{DISTRIBUTION};
  my $cpan_mod     = $module;
  next if $$info{FILES} !~ m{^(cpan|dist)/};
  next unless $distribution;
  my $prefix = $1;

  my @files         = glob $$info{FILES};
  if (!-d $files [0] || grep { $_ eq $module } @problematic) {
    say "This looks like a setup $0 cannot handle (yet)";
    unless ($force) {
        say "Will not continue without a --force option";
        exit 1;
    }
    say "--force is in effect, so we'll soldier on. Wish me luck!";
  }

  if (!$dryrun and $info->{CUSTOMIZED}) {
    print <<"EOF";
$module has a CUSTOMIZED entry in Porting/Maintainers.pl.

This program's behaviour is to copy every CUSTOMIZED file into the version
of the module being imported. But that might not be the right thing: in some
cases, the new CPAN version will supersede whatever changes had previously
been made in blead, so it would be better to import the new CPAN files.

If you've checked that the CUSTOMIZED versions are still correct, you can
proceed now. Otherwise, you should abort and investigate the situation. If
the blead customizations are no longer needed, delete the CUSTOMIZED entry
for $module in Porting/Maintainers.p. And you'll also need to regenerate
t/porting/customized.dat in that case via $^X t/porting/customized.t --regen.

EOF
    print "Hit return to continue; ^C to abort "; <STDIN>;
  }

  chdir $prefix;

  my  $pkg_dir      = $files[0];
      $pkg_dir      =~ s!.*/!!;

  my ($old_version) = $distribution =~ /-([0-9.]+(?:(-TRIAL|_)[0-9]*)?)\.(tar\.gz|tgz)$/;

  my  $o_module     = $module;
  if ($cpan_mod =~ /-/ && $cpan_mod !~ /::/) {
    $cpan_mod =~ s/-/::/g;
  }

  #
  # Find the information from CPAN.
  #
  my $new_file;
  my $new_version;
  unless ($tarball) {
    #
    # Poor man's cache
    #
    unless (-f $package_file && -M $package_file < 1) {
        eval {
            require HTTP::Tiny;
            my $http= HTTP::Tiny->new();
            $http->mirror( $package_url => $package_file );
            1
        } or system('wget', $package_url, '-qO', $package_file);
    }

    my $new_line;
    if ($^O eq 'MSWin32') {
      open my $fh, '<', $package_file;
      ($new_line) = grep {/^$cpan_mod/} <$fh> # Yes, this needs a lot of memory
        or die "Cannot find $cpan_mod on CPAN\n";
    } else {
      $new_line = `grep '^$cpan_mod ' $package_file`
        or die "Cannot find $cpan_mod on CPAN\n";
      chomp $new_line;
    }
    (undef, $new_version, my $new_path) = split ' ', $new_line;
    if (defined $version) {
        $new_path =~ s/-$new_version\./-$version\./;
        $new_version = $version;
    }
    $new_file = (split '/', $new_path) [-1];
    if ($new_file =~ /^perl-5\./) {
      say "Skip $new_path";
      chdir "..";
      next;
    }

    # see if we have this already
    if ($force or ($old_version lt $new_version)) {
      my $url = "http://search.cpan.org/CPAN/authors/id/$new_path";
      say "Update $url over $old_version";
      #
      # Fetch the new distro
      #
      if (! -f $new_file) {
        eval {
          require HTTP::Tiny;
          my $http= HTTP::Tiny->new();
          $http->mirror( $url => $new_file );
          1
        } or system('wget', $url, '-qO', $new_file);
      }
    } else {
      say "Already have latest $new_path";
      chdir "..";
      next;
    }
  }
  else {
    if (!$dryrun and (!-e $tarball or !-f _)) {
      warn "Tarball $tarball does not exist\n";
      chdir "..";
      next MODULES;
    }
    $new_file     = $tarball;
    $new_version  = $version // ($new_file =~ /-([0-9._]+(?:-TRIAL[0-9]*)?)\.(tar\.gz|tgz)/) [0];
    if ($new_version eq $old_version) {
      say "skip $module: Blead and that tarball both have version $new_version";
      chdir "..";
      next MODULES;
    }
  }

  my  $old_dir      = "$pkg_dir-$old_version";

  say "Cleaning out old directory $prefix/$pkg_dir";
  system('git', 'clean', '-dfxq', $pkg_dir) unless $dryrun;

  say "Unpacking $new_file";
  Archive::Tar->extract_archive( $new_file ) unless $dryrun;

  (my $new_dir = $new_file) =~ s/\.(tar\.gz|tgz)//;
  my (@EXCLUDED_QR, %EXCLUDED_QQ, @delete, @commit, @gone, @de_exec);
  if ($$info {EXCLUDED}) {
    foreach my $entry (@{$$info {EXCLUDED}}) {
        if (ref $entry) {push @EXCLUDED_QR => $entry}
        else            {$EXCLUDED_QQ {$entry} = 1}
    }
  }
  
  unless ($dryrun) {
    # ensure 'make' will update all files
    my $t = time;
  FILE:
    for my $file (find_type_f($new_dir)) {
      my ($base) = $file =~ m{$new_dir/(.*)$};
      next if $EXCLUDED_QQ{$base} or $IGNORABLE{$base};
      for my $qr (@EXCLUDED_QR) {
        next FILE if $base =~ $qr;
      }
      # How to deal with unwritable files?
      make_writable($file); # for convenience if the user later edits it
      open(my $fh, '>>', $file)
        || warn "Cannot write $file:$!";
      close($fh);
      utime($t,$t,$file);
    };

    if (-d $pkg_dir) {
      say "Renaming directory $prefix/$pkg_dir => $prefix/$old_dir";
      rename $pkg_dir => $old_dir;
    }
  }

  say "Creating new package directory $prefix/$pkg_dir";
  mkdir $pkg_dir unless $dryrun;

  say "Populating new package directory";
  my $map = $$info {MAP};

  if ($dryrun) {
    say "EXCLUDE: ", join(" ", keys %EXCLUDED_QQ),
    join(" ", @EXCLUDED_QR);
  } else {
  FILE: for my $file ( find_type_f( $new_dir )) {
    my $old_file = $file;
    $file =~ s{^$new_dir/}{};

    next if $EXCLUDED_QQ{$file};
    for my $qr (@EXCLUDED_QR) {
        next FILE if $file =~ $qr;
    }

    if ( $map ) {
        for my $key ( sort { length $b <=> length $a } keys %$map ) {
            my $val = $map->{$key};
            last if $file =~ s/^$key/$val/;
        }
    }
    else {
        $file = $files[0] . '/' . $file;
    }

    if ( $file =~ m{^(cpan|dist)/} ) {
        $file =~ s{^(cpan|dist)/}{};
    }
    else {
        $file = '../' . $file;
    }

    my $prefix = '';
    my @parts = split '/', $file;
    pop @parts;
    for my $part (@parts) {
        $prefix .= '/' if $prefix;
        $prefix .= $part;
        mkdir $prefix unless -d $prefix;
    }
    rename $old_file => $file;
  }

  remove_tree( $new_dir );

  if (-f "$old_dir/.gitignore") {
    say "Restoring .gitignore";
    system('git', 'checkout', "$pkg_dir/.gitignore");
  }

  my @new_files = find_type_f( $pkg_dir );
  @new_files = grep {$_ ne $pkg_dir} @new_files;
  s!^[^/]+/!! for @new_files;
  my %new_files = map {$_ => 1} @new_files;

  my @old_files = find_type_f( $old_dir );
  @old_files = grep {$_ ne $old_dir} @old_files;
  s!^[^/]+/!! for @old_files;
  my %old_files = map {$_ => 1} @old_files;

 FILE:
    foreach my $file (@new_files) {
      next if -d "$pkg_dir/$file";   # Ignore directories.
      next if $old_files {$file};    # It's already there.
      if ($IGNORABLE {$file}) {
          push @delete => $file;
          next;
      }
      push @commit => $file;
    }
    foreach my $file (@old_files) {
      next if -d "$old_dir/$file";
      next if $new_files {$file};
      push @gone => $file;
    }
  
    #
    # Find all files with an exec bit
    #
    my @exec = find_type_f( $pkg_dir );
    foreach my $file (@exec) {
      # Remove leading dir
      $file =~ s!^[^/]+/!!;
      if ($file =~ m!^t/!) {
          push @de_exec => $file;
          next;
      }
      # Check to see if the file exists; if it doesn't and doesn't have
      # the exec bit, remove it.
      if ($old_files {$file}) {
          unless (-x "$old_dir/$file") {
              push @de_exec => $file;
          }
      }
    }
  } # end of dryrun

  #
  # No need to change the +x bit on files that will be deleted.
  #
  if (@de_exec && @delete) {
    my %delete = map {+"$pkg_dir/$_" => 1} @delete;
    @de_exec = grep {!$delete {$_}} @de_exec;
  }
  #
  # Mustn't change the +x bit on files that are whitelisted
  #
  if (@de_exec) {
      my %permitted = map +(tr/\n//dr => 1), grep !/^#/,
        do { local @ARGV = '../Porting/exec-bit.txt'; <> };
      @de_exec = grep !$permitted{"cpan/$pkg_dir/$_"}, @de_exec;
  }

  if (!$dryrun) {
    say "unlink $pkg_dir/$_" for @delete;
    say "git add $pkg_dir/$_" for @commit;
    say "git rm -f $pkg_dir/$_" for @gone;
    say "chmod a-x $pkg_dir/$_" for @de_exec;
    
    print "Hit return to continue; ^C to abort "; <STDIN>;

    unlink "$pkg_dir/$_"                     for @delete;
    say "git add $pkg_dir/$_" 		     for @commit;
    system('git', 'add', "$pkg_dir/$_")      for @commit;
    system('git', 'rm', '-f', "$pkg_dir/$_") for @gone;
    de_exec( "$pkg_dir/$_" )                 for @de_exec;
  }

  #
  # Restore anything that is customized.
  # We don't really care whether we've deleted the file - since we
  # do a git restore, it's going to be resurrected if necessary.
  #
  if (!$dryrun and $$info{CUSTOMIZED}) {
    say "Restoring customized files";
    foreach my $file (@{$$info{CUSTOMIZED}}) {
        system('git', "checkout", "$pkg_dir/$file");
    }
  }

  chdir "..";
  if (!$dryrun and (@commit || @gone)) {
    say "Fixing MANIFEST";
    my $MANIFEST     = "MANIFEST";
    my $MANIFEST_NEW = "$MANIFEST.new";

    open my $orig, "<", $MANIFEST
        or die "Failed to open $MANIFEST for reading: $!\n";
    open my $new, ">", $MANIFEST_NEW
        or die "Failed to open $MANIFEST_NEW for writing: $!\n";
    my %gone = map +("$prefix/$pkg_dir/$_" => 1), @gone;
    while (my $line = <$orig>) {
        my ($file) = $line =~ /^(\S+)/
            or die "Can't parse MANIFEST line: $line";
        print $new $line if !$gone{$file};
    }

    say $new "$prefix/$pkg_dir/$_" for @commit;

    close $new or die "Can't close $MANIFEST: $!\n";

    system $^X => "Porting/manisort", '--quiet',
                  "--output=$MANIFEST", $MANIFEST_NEW;
    unlink $MANIFEST_NEW
        or die "Can't delete temporary $MANIFEST_NEW: $!\n";
  }

  print "Update $maintainers ";
  open my $Maintainers_pl, '<', $maintainers;
  open my $new_Maintainers_pl, '>', $maintainers.'.new';
  my $found;
  my $in_mod_section;
  while (<$Maintainers_pl>) {
    if (!$found) {
      if ($in_mod_section) {
        if (/DISTRIBUTION/) {
          if (s/\Q$old_version/$new_version/) {
            $found = 1;
          }
        }
        if (/^    }/) {
          $in_mod_section = 0;
        }
      }
      if (/\Q$cpan_mod/) {
        $in_mod_section = 1;
      }
    }
    print $new_Maintainers_pl $_;
  }
  close $Maintainers_pl;
  close $new_Maintainers_pl;
  if ($dryrun) {
    my $cmd = "diff -bu $maintainers $maintainers.new";
    say $cmd;
    say `$cmd`;
  }
  elsif ($found) {
    unlink $maintainers;
    rename "$maintainers.new" => $maintainers;
    chmod 0755 => $maintainers;
    say "ok";
  }
  else {
    say "failed.";
    say "Make sure you update $maintainers before committing.";
  }

  my $perldelta = -f 'STATUS.md' ? 'pod/perlcdelta.pod' : 'pod/perldelta.pod';
  print "Update $perldelta ";
  open my $delta, '<', $perldelta;
  open my $newdelta, '>', $perldelta.'.new';
  ($found, $in_mod_section) = (0,0);
  while (<$delta>) {
    my $mod;
    if (!$found) {
      if ($in_mod_section) {
        # sorted
        if (/^=item L<(.*)>/) {
          $mod = $1;
          if ($mod eq $cpan_mod) {
            if (s/\Q$old_version/$new_version/) {
              $found = 1;
            }
          } elsif (fc($mod) gt fc($cpan_mod)) {
            print $newdelta "=item L<$cpan_mod>  $new_version\n\n";
            $found = 1;
          }
        }
        if (/^=head/) {
          $in_mod_section = 0;
        }
      }
      if (/^=head2 Updated Modules/) {
        $in_mod_section = 1;
      }
    }
    print $newdelta $_;
  }
  close $delta;
  close $newdelta;
  if ($dryrun) {
    my $cmd = "diff -bu $perldelta $perldelta.new";
    say $cmd;
    say `$cmd`;
  }
  elsif ($found) {
    unlink $perldelta;
    rename "$perldelta.new" => $perldelta;
    chmod 0644 => $perldelta;
    say "ok";
  }
  else {
    say " failed.";
    say "Make sure you update $perldelta before committing.";
  }

  # TODO: which modules were changed? and to which version?
  # so far we change only one
  update_corelist($cpan_mod, $new_version);

  if ($make_c) {
    print "Running make -C $prefix/$pkg_dir ... ";
    make '-C $prefix/$pkg_dir' unless $dryrun;
  } else {
    print "Running make test-prep ... ";
    # Prepare for running (selected) tests
    make 'test-prep' unless $dryrun;
  }
  print "done\n";

  # The build system installs code from CPAN dists into the lib/ directory,
  # creating directories as needed. This means that the cleaning-related rules
  # in the Makefile need to know which directories to clean up. The Makefile
  # is generated by Configure from Makefile.SH, so *that* file needs the list
  # of directories. regen/lib_cleanup.pl is capable of automatically updating
  # the contents of Makefile.SH (and win32/Makefile, which needs similar but
  # not identical lists of directories), so we can just run that (using the
  # newly-built Perl, as is done with the regen programs run by "make regen").
  #
  # We do this if any files at all have been added or deleted, regardless of
  # whether those changes result in any directories being added or deleted,
  # because the alternative would be to replicate the regen/lib_cleanup.pl
  # logic here. That's fine, because regen/lib_cleanup.pl is idempotent if run
  # repeatedly.
  if (@commit || @gone) {
    say "Running regen/lib_cleanup.pl to handle potential added/deleted dirs";
    my $exe_dir = WIN32 ? ".\\" : './';
    if ($dryrun) {
      print "${exe_dir}perl$Config{_exe} -Ilib regen/lib_cleanup.pl\n";
    } else {
      system "${exe_dir}perl$Config{_exe}", "-Ilib", "regen/lib_cleanup.pl"
        and die "regen/lib_cleanup.pl failed\n";
    }
  }

  #
  # Must clean up, or else t/porting/FindExt.t will fail.
  # Note that we can always retrieve the original directory with a git checkout.
  #
  if (!$dryrun) {
    system('git','diff', $prefix);
    system('git','status', "$prefix/$pkg_dir");
    print "About to clean up; hit return or abort (^C) "; <STDIN>;

    remove_tree( "$prefix/$old_dir" );
    unlink "$prefix/$new_file" unless $tarball;

    #
    # Run the tests. First the test belonging to the module, followed by the
    # the tests in t/porting
    #
    chdir "t";
    say "Running module tests";
    my $exe_dir = $^O =~ /MSWin/ ? "..\\" : './';
    my @test_files = grep { /\.t$/ } find_type_f( "../$prefix/$pkg_dir" );
    $ENV{TEST_JOBS} = $_ for grep defined, $make_jobs;
    if (@test_files) {
      my $output = `${exe_dir}perl$Config{_exe} -I../lib harness @test_files`;
      unless ($output =~ /All tests successful/) {
        say $output;
        exit 1;
      }
    }
    
    print "Running tests in t/porting ";
    my @tests = glob 'porting/*.t';
    chomp @tests;
    my @failed;
    foreach my $t (@tests) {
      my @not = grep {!/# TODO/ }
      grep { /^not/ }
      `${exe_dir}perl -I../lib -I.. $t`;
      print @not ? '!' : '.';
      push @failed => $t if @not;
    }
    print "\n";
    say "Failed tests: @failed" if @failed;
    chdir '..';
  }

  say "$o_module is now version $new_version";
  if ($all and !$dryrun) {
    print "About to process next module; hit return or abort (^C) "; <STDIN>;
  }
}

print <<"EOF" unless $dryrun;

=======================================================================

Next, you should run a "make test".

Hopefully that will complete successfully, but if not, you can make any
changes you need to get the tests to pass. Don't forget that you'll need
a "CUSTOMIZED" entry in Porting/Maintainers.pl if you change any of the
files.

Once all tests pass, you can "git add -u" and "git commit" the changes.

EOF

__END__
